/TELA RESIDENT
ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ ___________________________________   RELATORIO APURACAO ICMS    __/__/____  บ
ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ                                                                              บ
บ                                                                              บ
บ  Codigo da Empresa.........: [_.]  [_____________________________________]   บ
บ                                                                              บ
บ  Data Inicial do mes ......: [__/__/____]                                    บ
บ                                                                              บ
บ  Data Final do Mes ........: [__/__/____]                                    บ
บ                                                                              บ
บ  Pagina Inicial ...........: [__.]                                           บ
บ                                                                              บ
บ                                                                              บ
บ                                                                              บ
บ                                                                              บ
บ                                                                              บ
บ                                                                              บ 
บ                                                                              บ 
ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ  < F1 > AJUDA   < F5 > LIMPA TELA                         CONFIRMA [S/N] _   บ
ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
/HELP
ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ                                                                              บ 
บ                         AJUDA AO USUARIO                                     บ
บ                         ================                                     บ
บ                                                                              บ
บ                                                                              บ
บ      Este relatorios tem objetivo de informar os  valores  referente  ao     บ
บ  icms do lancamento de entrada e de saida por cfo.  Mostrando o Vlr Con-     บ
บ  tab, Base Calc, Imposto, Isentas, Outras. No Final um resumo do Demons-     บ
บ  trativo de Credito e Debito.                                                บ
บ      No final do relatorio Perguntara se Quer atualizar os saldo, se res-    บ
บ  ponder que Sim ele criara um lancamento com um mes apos a data Final no     บ  
บ  lancamento de apuracao.                                                     บ
บ      Formulario : 80 Colunas                                                 บ
บ                                                                              บ
บ                                                                              บ
บ                                                                              บ
ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ   PRESSIONE ALGO PARA CONTINUAR                                              บ
ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
/HEADER
================================================================================================================
EMPRESA .: ___________________________________                                                 FOLHA .:   ____0.
REGISTRO  DE  APURACAO  DO ICMS                                           PERIODO .: __/__/____  ATE  __/__/____
================================================================================================================
CFO    DESCRICAO                               VLR. CONT     BASE CALC       IMPOSTO       ISENTAS        OUTRAS
/SUBHEADER1

                                                                        _________________

/BODY
___.   ___________________________________   ____,___.__   ____,___.__   ____,___.__   ____,___.__   ____,___.__
/SUBTOTAL2
___.   ___________________________________   ____,___.__   ____,___.__   ____,___.__   ____,___.__   ____,___.__
/SUBTOTAL1

T O T A L    =================  >>           ____,___.__   ____,___.__   ____,___.__   ____,___.__   ____,___.__
/TOTAL


          DEMONSTRATIVO  DE  CREDITO                              DEMONSTRATIVO  DE  DEBITO

POR ENTRADAS C/CREDITOS DE IMPOSTO ===>  _____,___.__         POR SAIDAS C/DEBITOS DE IMPOSTO ===>  _____,___.__
OUTROS CREDITOS ======================>  _____,___.__         OUTROS DEBITOS ====================>  _____,___.__
ESTORNOS DE DEBITOS ==================>  _____,___.__         ESTORNOS DE CREDITOS ==============>  _____,___.__
SUB  -  TOTAL ========================>  _____,___.__         TOTAL =============================>  _____,___.__
SALDO CREDOR PERIODO ANTERIOR ========>  _____,___.__
TOTAL  ===============================>  _____,___.__

                                              APURACAO DE SALDOS

SALDO DEVEDOR ========================>  _____,___.__      DEDUCOES ==========> _____,___.__
IMPOSTO A RECOLHER ===================>  _____,___.__      SALDO CREDOR ======> _____,___.__

____           _________________________________________________________
               _________________________________________________________
               _________________________________________________________
               _________________________________________________________

/*
#INCLUDE OBJETOS.LIB
STRING  VARQTEMP

NAME TEMPRE TDATA TLOJA TDLOJA TDT_INI TDT_FIN TPAG TCONF

NUMBER VT1VL_CONTAB VT1BC_ICM VT1IMPICM VT1ISENICM VT1OUTICM
NUMBER VANO VMES VDIA

NUMBER VTOTALICMS VSALDOICMS VSALDOTOTAL SUBTOCRE SUBTODEV VSALDO VTIMPREC
NUMBER VALORICMS VOUTC_ICMS VOUTD_ICMS VESTC_ICMS VESTD_ICMS VDEDICMS VICMS1
NUMBER VVTIMPREC VVALOR1 VSALOBS VTIPO

STRING VDESCR1

PAGE SET HELP AT 3 0

OPEN CPPARAM
OPEN LFLANENT
OPEN LFPARAM
OPEN LFCADCFO
OPEN LFLANSAD
OPEN LFAPUOBS
OPEN LFAPURA
OPEN LFTEMAPU

FILE_MODE CPPARAM READ_ONLY
FILE_MODE LFPARAM READ_ONLY
FILE_MODE LFLANENT READ_ONLY
FILE_MODE LFCADCFO READ_ONLY
FILE_MODE LFLANSAD READ_ONLY
FILE_MODE LFAPUOBS READ_ONLY

STRING VESC 1 VESC1 1

CLEAR CPPARAM
FIND GT CPPARAM BY INDEX.1
PRINT CPPARAM.RA_SOCIAL TO TEMPRE
SYSDATE TDATA

ENTERGROUP
  INICIO:
   CLEARFORM TLOJA THRU TCONF
   AUTOPAGE TELA 3

   ENTRY LFPARAM.CODIGO {AUTOFIND}
   INDICATE CERTO AS [ FOUND]
   [~CERTO] ENTRY LFPARAM.RAZAO  {CAPSLOCK,AUTOFIND}
   [~FOUND] GOTO INICIO
   
   ACCEPT TDT_INI
   ACCEPT TDT_FIN {REQUIRED}

   ACCEPT TPAG

   MOVE "S" TO TCONF
   ACCEPT TCONF {CAPSLOCK}
   IF TCONF NE "S" GOTO INICIO   
   
ENDGROUP

GOTOXY 18 3
SHOW "P R O C E S S A N D O,  A G U A R D E ..."

REREAD
ZEROFILE LFTEMAPU
REREAD

MOVE 0 TO VTOTALICMS

CLEAR LFLANENT
MOVE TLOJA   TO LFLANENT.LOJA
MOVE TDT_INI TO LFLANENT.DENTRADA 
FIND GT LFLANENT BY INDEX.3
[ FOUND] INDICATE FOUND AS LFLANENT.LOJA     EQ TLOJA
[ FOUND] INDICATE FOUND AS LFLANENT.DENTRADA LE TDT_FIN
WHILE [ FOUND]
   CLEAR LFTEMAPU
   MOVE TLOJA             TO LFTEMAPU.LOJA
   MOVE LFLANENT.NUMLANC  TO LFTEMAPU.NUM_LANC
   MOVE LFLANENT.CCFO     TO LFTEMAPU.CFO
   MOVE LFLANENT.ALIQUOTA TO LFTEMAPU.ALIQUOTA
   MOVE "E"               TO LFTEMAPU.TIPO
   SAVERECORD LFTEMAPU

   CALC (VTOTALICMS + LFLANENT.VL_IMPOSTO) TO VTOTALICMS
  
   FIND GT LFLANENT BY INDEX.3
   [ FOUND] INDICATE FOUND AS LFLANENT.LOJA     EQ TLOJA
   [ FOUND] INDICATE FOUND AS LFLANENT.DENTRADA LE TDT_FIN  
END

CLEAR LFLANSAD
MOVE TLOJA   TO LFLANSAD.LOJA
MOVE TDT_INI TO LFLANSAD.DT_SAIDA
FIND GT LFLANSAD BY INDEX.2
[ FOUND] INDICATE FOUND AS LFLANSAD.LOJA     EQ TLOJA
[ FOUND] INDICATE FOUND AS LFLANSAD.DT_SAIDA LE TDT_FIN  
WHILE [ FOUND]
   
   CLEAR LFTEMAPU
   MOVE TLOJA            TO LFTEMAPU.LOJA
   MOVE LFLANSAD.NUM_DOC TO LFTEMAPU.NUM_DOC
   MOVE LFLANSAD.SERIE   TO LFTEMAPU.SERIE
   MOVE LFLANSAD.CFO     TO LFTEMAPU.CFO
   MOVE LFLANSAD.ALIICM  TO LFTEMAPU.ALIQUOTA
   MOVE "S"          TO LFTEMAPU.TIPO
   SAVERECORD LFTEMAPU
      
   CALC (VT1VL_CONTAB + LFLANSAD.VL_CONTAB) TO VT1VL_CONTAB
   CALC (VT1BC_ICM    + LFLANSAD.B_C_ICM)   TO VT1BC_ICM
   CALC (VT1IMPICM    + LFLANSAD.IMPICM)    TO VT1IMPICM
   CALC (VT1ISENICM   + LFLANSAD.ISENICM)   TO VT1ISENICM
   CALC (VT1OUTICM    + LFLANSAD.OUTICM)    TO VT1OUTICM

   FIND GT LFLANSAD BY INDEX.2
   [ FOUND] INDICATE FOUND AS LFLANSAD.LOJA     EQ TLOJA
   [ FOUND] INDICATE FOUND AS LFLANSAD.DT_SAIDA LE TDT_FIN     
END

GOTOXY 18 3
SHOW "I M P R I M I N D O,  A G U A R D E ...." 

MOVE (CRIA_ARQ_TEMP("")) TO VARQTEMP
OUTFILE VARQTEMP

CHARACTER 27 TO VESC 

LEFT  TDT_INI TO VDIA 2
RIGHT TDT_INI TO VANO 4
MID TDT_INI  TO VMES 2 4

//WRITE VESC "[4"

IF TPAG GT 0 MOVE (TPAG - 1) TO PAGECOUNT

CLEAR LFTEMAPU
REPORT LFTEMAPU BY INDEX.1 BREAK LFTEMAPU.TIPO LFTEMAPU.CFO
   
    SECTION HEADER
       PRINT TDLOJA
       PRINT PAGECOUNT
       PRINT TDT_INI
       PRINT TDT_FIN
    OUTPUT HEADER
    
    SECTION SUBHEADER1
       IF LFTEMAPU.TIPO EQ "E" PRINT "E N T R A D A" TO SUBHEADER1.1
       ELSE PRINT "  S A I D A" TO SUBHEADER1.1
    OUTPUT SUBHEADER1
    
    SECTION BODY
       PRINT LFTEMAPU.CFO
       FINDKEY EQ LFCADCFO BY INDEX.1 FOR LFCADCFO.CODIGO EQ LFTEMAPU.CFO
       PRINT LFCADCFO.DESCR
       
       IF LFTEMAPU.TIPO EQ "E" BEGIN
          FINDKEY EQ LFLANENT BY INDEX.1 FOR LFLANENT.LOJA     EQ LFTEMAPU.LOJA;
                                             LFLANENT.NUMLANC  EQ LFTEMAPU.NUM_LANC;
                                             LFLANENT.ALIQUOTA EQ LFTEMAPU.ALIQUOTA

          PRINT LFLANENT.VL_CONTABI
          PRINT LFLANENT.VL_BCICMS
          PRINT LFLANENT.VL_IMPOSTO
          PRINT LFLANENT.VL_ISENTAS
          PRINT LFLANENT.VL_OUTRAS                                              
       END                                           
       ELSE BEGIN
          FINDKEY EQ LFLANSAD BY INDEX.1 FOR LFLANSAD.LOJA    EQ LFTEMAPU.LOJA;
                                             LFLANSAD.NUM_DOC EQ LFTEMAPU.NUM_DOC;
                                             LFLANSAD.SERIE   EQ LFTEMAPU.SERIE;
                                             LFLANSAD.ALIICM  EQ LFTEMAPU.ALIQUOTA

          PRINT LFLANSAD.VL_CONTAB TO BODY.3
          PRINT LFLANSAD.B_C_ICM   TO BODY.4 
          PRINT LFLANSAD.IMPICM    TO BODY.5
          PRINT LFLANSAD.ISENICM   TO BODY.6
          PRINT LFLANSAD.OUTICM    TO BODY.7
       END    

    SECTION SUBTOTAL1
       SUBTOTAL SUBTOTAL2.3
       SUBTOTAL SUBTOTAL2.4
       SUBTOTAL SUBTOTAL2.5
       SUBTOTAL SUBTOTAL2.6
       SUBTOTAL SUBTOTAL2.7
    OUTPUT SUBTOTAL1   

    SECTION SUBTOTAL2
       PRINT BODY.1
       PRINT BODY.2
       SUBTOTAL BODY.3
       SUBTOTAL BODY.4
       SUBTOTAL BODY.5
       SUBTOTAL BODY.6
       SUBTOTAL BODY.7
    OUTPUT SUBTOTAL2
    
    SECTION TOTAL
       BLANKFORM TOTAL
       
       FINDKEY EQ LFAPURA BY INDEX.1 FOR LFAPURA.EMPRESA EQ TLOJA;
                                         LFAPURA.MES     EQ VMES;
                                         LFAPURA.ANO     EQ VANO
                                         
       MOVE LFAPURA.VALORICMS TO VICMS1
       MOVE LFAPURA.OUTC_ICMS TO VOUTC_ICMS
       MOVE LFAPURA.OUTD_ICMS TO VOUTD_ICMS
       MOVE LFAPURA.ESTC_ICMS TO VESTC_ICMS
       MOVE LFAPURA.ESTD_ICMS TO VESTD_ICMS
       MOVE LFAPURA.DEDICMS   TO VDEDICMS

       PRINT VTOTALICMS
       PRINT VT1IMPICM
       PRINT VOUTC_ICMS
       PRINT VOUTD_ICMS
       PRINT VESTD_ICMS
       PRINT VESTC_ICMS

       CALC (VTOTALICMS + VOUTC_ICMS + VESTD_ICMS) TO SUBTOCRE
       CALC (VT1IMPICM  + VOUTD_ICMS + VESTC_ICMS) TO SUBTODEV

       PRINT SUBTOCRE
       PRINT SUBTODEV
       PRINT VICMS1

       CALC (SUBTOCRE    + VICMS1)    TO VSALDOTOTAL
       CALC (VSALDOTOTAL - SUBTODEV ) TO VSALDOICMS

       PRINT VSALDOTOTAL

       MOVE (ABS(VSALDOICMS)) TO VVALOR1
       MOVE 0 TO VDEDICMS
       MOVE VSALDOICMS TO VSALOBS

       IF VSALDOICMS LT 0 BEGIN
          PRINT VVALOR1  TO TOTAL.11
          CALC ((VVALOR1 * LFPARAM.PERC) / 100) TO VDEDICMS
       END
       ELSE PRINT VSALDO TO TOTAL.11

       IF VSALDOICMS LT 0 PRINT VDEDICMS TO TOTAL.12
       ELSE PRINT VSALDO TO TOTAL.12

       CALC (VVALOR1 - VDEDICMS) TO VTIMPREC

       IF VSALDOICMS LT 0 BEGIN
          MOVE (ABS(VTIMPREC)) TO VVTIMPREC
          PRINT VVTIMPREC  TO TOTAL.13
       END
       ELSE PRINT VSALDO   TO TOTAL.13
     
       IF VSALDOICMS GT 0 PRINT VVALOR1  TO TOTAL.14
       ELSE PRINT VSALDO   TO TOTAL.14

       // ****** O B S ****

       MOVE 1 TO VTIPO
       FINDKEY GE LFAPUOBS BY INDEX.1 FOR LFAPUOBS.LOJA   EQ TLOJA; //EQ
                                          LFAPUOBS.MES    EQ VMES;
                                          LFAPUOBS.ANO    EQ VANO;
                                          LFAPUOBS.TIPO   EQ VTIPO

             PRINT "OBS:"  TO TOTAL.15

             PRINT LFAPUOBS.DESCR1 TO TOTAL.16
             PRINT LFAPUOBS.DESCR2 TO TOTAL.17
             PRINT LFAPUOBS.DESCR3 TO TOTAL.18
             PRINT LFAPUOBS.DESCR4 TO TOTAL.19
       OUTPUT TOTAL

       // ***** A T U A L I Z A C A O     D O S   S A L D O S ******

       CLEARXY SCREENEND 00
       SHOW "ATUALIZA OS SALDOS"
       MOVE "" TO TCONF
       ACCEPT TCONF {CAPSLOCK}
       IF TCONF EQ "S" BEGIN
          MOVE (VMES + 1) TO VMES
          IF VMES GT 12 BEGIN
             MOVE 01 TO VMES
             MOVE (VANO + 1) TO VANO
          END

          FINDKEY EQ LFAPURA BY INDEX.1 FOR LFAPURA.EMPRESA EQ TLOJA;
                                            LFAPURA.MES     EQ VMES;
                                            LFAPURA.ANO     EQ VANO
         REREAD 
          IF VSALDOICMS GT 0 MOVE VSALDOICMS TO LFAPURA.VALORICMS
          ELSE MOVE 0 TO LFAPURA.VALORICMS
          SAVERECORD LFAPURA
         UNLOCK
       END 
REPORTEND
FORMFEED
OUTCLOSE
UNLOCK
CLEARXY SCREENEND 00
SEND READ TO USER_OUTPUT VARQTEMP
SEND Display_Report  TO User_Output
ERASEFILE VARQTEMP
CLEARSCREEN
GOTO INICIO

// ***** T  E C L A S

KEYPROC KEY.ESCAPE
  CLEARSCREEN
  ABORT

KEYPROC KEY.CLEAR
RETURN INICIO

KEYPROC KEY.PRINT
KEYPROC KEY.DELETE
KEYPROC KEY.HELP
KEYPROC KEY.USER
KEYPROC KEY.USER2
KEYPROC KEY.SAVE
  ENTAGAIN
RETURN

