/TELA
ษอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ ________________________________ ALTER. DO LANCAMENTO DE ENTRADA __/__/____ บ
ศอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
ษอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ Empresa ........: _.  ___________________________________                   บ
บ Lancamento .....: _____.                                                    บ
บ Cod. Aliquota ..: _. ___.__ ___.__ ___.__         Nota Fiscal..: ________.  บ
บ Fornecedor .....: ___. ___________________________________                  บ
บ C.F.O. .........: ___. ___________________________________                  บ
บ Especie/Serie ..: ___  ___                        Modelo .: _.              บ
บ Data Entrada ...: __/__/____                                                บ
บ Data Emissao ...: __/__/____              Classificacao ..: _________.      บ
บ Un. Federacao...: _. ____________________ Unidade ........: __              บ
บ Tip. Lanc. CTB .: ___. __________________ Quantidade .....: _________.__    บ
บ Base Calc Icms .: _________.__            Base Calc IPI ..: _________.__    บ
บ Vlr. Isentas ...: _________.__            Vlr. Isentas ...: _________.__    บ
บ Vlr. Outras ....: _________.__            Vlr. Outras ....: _________.__    บ
บ Imposto ........: _________.__            Aliquota .......: __.__           บ
บ Vlr. Contabel ..: _________.__            Imposto ........: _________.__    บ
บ Tipo ...........: _                       Vlr. Art. 82 ...: _________.__    บ
บ Observacao .....: ___.  ___________________________________                 บ
บ Codigo DIEF ....: _.    __________________________________________________  บ
ฬอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออน
บ  < F1 > AJUDA  < F5 > LIMPA TELA  < F3 > Cad. Auxiliar    CONFIRMA [S/N] _  บ
ศอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
/HELP
ษอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ                                                                             บ
บ                        AJUDA AO USUARIO                                     บ
บ                        ================                                     บ
บ                                                                             บ
บ     Aqui voce devera Alterar os Lancamentos de Entrada, caso algum erro     บ
บ  de digitacao foi feito na ora de lancar ou algum acerto nos valores. A     บ
บ  coluna da esquerda voce devera digitar os valores do Icms e da direita     บ
บ  os valores de IPI, mas so entrara na digitacao do IPI se no cadastro de    บ
บ  empresa estiver no campo Usa IPI de Entrada (Sim), caso contrario  nao     บ
บ  Entrara. Para Sair da Digitacao do IPI digite (0) no campo da  Classi-     บ
บ  ficacao. Se voce quiser deletar algum lancto do IPI tecle "F6" na parte    บ
บ  da digitacao do IPI.                                                       บ
บ    Caso voce nao saiba algum codigo cadastrado digite um codigo invali-     บ
บ  do e informe a descricao em seguida use as teclas Page UP e  Page Down     บ
บ  para procurar a descricao correta.                                         บ
บ                                                                             บ
ศอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
ษอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ   PRESSIONE ALGO PARA CONTINUAR                                             บ
ศอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
/TDESCR_AUX
ษอออออออออออออออออออออออออออออออออออออป
บ DESCRICAO                           บ
วฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤถ
บ ___________________________________ บ
ศอออออออออออออออออออออออออออออออออออออผ
/HELP1
ษอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ Tabela de modelos de documentos fiscais                           บ
บ                                                                   บ
บ 24 - Autorizaฦo de Carregamento e Transporte, modelo 24.         บ
บ 14 - Bilhete de Passagem Aquavirio, modelo 14.                   บ
บ 15 - Bilhete de Passagem e Nota de bagagem, modelo 15.            บ
บ 16 - Bilhete de Passagem Ferrovirio, modelo 16.                  บ
บ 13 - Bilhete de Passagem Rodovirio, modelo 13.                   บ
บ 10 - Conhecimento Areo, modelo 10.                               บ
บ 11 - Conhecimento de Transporte Ferrovirio de Cargas, modelo 11. บ
บ 09 - Conhecimento de Transporte Aquavirio de Cargas, modelo 09.  บ
บ 08 - Conhecimento de Transporte Rodovirio de Cargas, modelo 08.  บ
บ 17 - Despacho de Transporte, modelo 17.                           บ
บ 25 - Manifesto de Carga, modelo 25.                               บ
บ 01 - Nota Fiscal, modelo 01.                                      บ
บ 06 - Nota Fiscal/Conta de Energia Eltrica, modelo 6.             บ
บ 03 - Nota Fiscal de Entrada, modelo 3.                            บ
บ 21 - Nota Fiscal de Servio de Comunicaฦo, modelo 21.            บ
บ 04 - Nota Fiscal de Produtor, modelo 4.                           บ
บ 22 - Nota Fiscal de Servio de Telecomuniไes, modelo 22.         บ
บ                                                                   บ
บ Tecle ENTER para visualizar mais itens...                         บ
ศอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
/HELP2
ษอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ Tabela de modelos de documentos fiscais                           บ
บ                                                                   บ
บ 07 - Nota Fiscal de Servio de Transporte, modelo 7.              บ
บ 02 - Nota Fiscal de Venda a Consumidor, modelo 2.                 บ
บ 20 - Ordem de Coleta de Carga, modelo 20.                         บ
บ 18 - Resumo Movimento Dirio, modelo 18.                          บ
บ 55 - Nota fiscal eletrnica                                       บ
บ 57 - Conhecimento de frete eletronico                             บ
บ                                                                   บ
บ                                                                   บ
บ                                                                   บ
บ                                                                   บ
บ                                                                   บ
บ                                                                   บ
บ                                                                   บ
บ                                                                   บ
บ                                                                   บ
บ                                                                   บ
บ                                                                   บ
บ                                                                   บ
บ Tecle ENTER para voltar do lancamento.....                        บ
ศอออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
/IMG_DADOS_SUBST_TRIB
ษออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออป
บ                                                              บ
บ Valores referentes a notas fiscais que possuem produtos com  บ
บ substituicao tributaria                                      บ
บ                                                              บ
บ Valor do IPI ............................: ____________.__   บ
บ Despesas acessorias .....................: ____________.__   บ
บ Base de calculo ICMS proprio ............: ____________.__   บ
บ ICMS proprio ............................: ____________.__   บ
บ Base calculo ICMS substituicao tributaria: ____________.__   บ
บ                                                              บ
บ Confirma dados acima? ...................: _                 บ
ศออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผ
/TELA_CAD_AUX
ษออออออออออออออออออออออออออออออออออออออออออออป
บ           Cadastros Auxiliares             บ
ฬออออออออออออออออออออออออออออออออออออออออออออน
บ                                            บ
บ <1> Dados Substituicao Tributaria          บ
บ                                            บ
บ                                            บ
บ Digite a Opcao ฤฤฤ>  [_]       ESC - Volta บ
ศออออออออออออออออออออออออออออออออออออออออออออผ
/*
#INCLUDE ROTINAS.LIB
NAME TEMPRE TDATA TLOJ TDLOJ TLANC TALIQ TALQ1 TALQ2 TALQ3 TNF TFORN TDFORN
NAME TCFO TDCFO TESP TSER TMODELO TDT_ENT TDT_EMIS TCLASS TEST TDEST TUN
NAME TTIPCTB TDTIPCTB TQTD TBASE_ICM TBASE_IPI TISEN_ICM TISEN_IPI TOUT_ICM
NAME TOUT_IPI TIMP_ICM TALIQ_IPI TVLR_CONT TIMP_IPI
NAME TTIPO TVLR_ART TOBS TDOBS TCODDIE TDDIE TCONF

NAME IMG_DADOS_SUBST_TRIB TVLRIPI TDESPACESS TBCICMSPROP TICMSPROP TBCSUBSTRIB
NAME TCONF_SUBST_TRIB

OPEN LFCADCFO
OPEN LFCADDIE
OPEN LFTIPCTB
OPEN AVCADEST
OPEN LFLANENT
OPEN LFPARAM
OPEN LFLANENI
OPEN CPCADFOR
OPEN LFLANCA2
OPEN LFLANCA1
OPEN LFCADDIE
OPEN LFCLAFIS 
OPEN LFOBSLAN
OPEN LFPARAM
OPEN CPPARAM

INTEGER CODALI X VLOJA
NUMBER VLANC1 VNUMLANC VBICMS VDIA1 VDIA VMES VANO VTCALIPI VTISENTA
NUMBER VTOUTRA VTIMP AC_VL_ISENTAS AC_BC_IPI AC_VL_OUTRAS AC_VL_ART82
NUMBER AC_VL_IMPOSTO VTBCICMS VTCONTABI VTISENTAS VTOUTRAS VTIMPOSTO
NUMBER VLANC VBASE
STRING VDESCR VREDUZ

PAGE SET HELP AT 3 0
PAGE SET TDESCR_AUX AT 8 30
PAGE SET HELP1 AT 01 5 COLORS 79 79
PAGE SET HELP2 AT 01 5 COLORS 79 79
PAGE SET TELA_CAD_AUX AT 08 10 COLORS 79 79
PAGE SET IMG_DADOS_SUBST_TRIB AT 5 5 COLORS 48 48 

CLEAR CPPARAM
FIND GT CPPARAM BY INDEX.1
PRINT CPPARAM.RA_SOCIAL TO TEMPRE
SYSDATE TDATA

ENTER LFLANENT 
  INICIO:
   INDICATE ENT$QUERY FALSE 
   CLEARFORM TLOJ THRU TCONF
   AUTOPAGE TELA 3   
   
   ENTRY LFPARAM.CODIGO         {RETAIN,AUTOFIND}
   INDICATE CERTO AS [ FOUND]
   [~CERTO] ENTRY LFPARAM.RAZAO {AUTOFIND,CAPSLOCK,RETAIN}
   [~FOUND] GOTO INICIO
   
   ENTRY LFLANENT.NUMLANC 
    
  ALIQUOTA:
   ENTRY LFLANCA2.CODIGO {AUTOFIND}
   [~FOUND] BEGIN
     ERROR 999 "ALIQUOTA NAO CADASTRADA, TECLE ALGO"
     PAUSE ""
     GOTO ALIQUOTA
   END

   ENTRY LFLANCA2.ALQ  {DISPLAYONLY}
   ENTRY LFLANCA2.ALQ1 {DISPLAYONLY}
   ENTRY LFLANCA2.PERC {DISPLAYONLY}

   MOVE LFLANCA2.CODIGO TO CODALI
   MOVE LFLANCA2.REDUZ  TO VREDUZ

   FINDKEY EQ LFLANENT BY INDEX.1 FOR LFLANENT.LOJA     EQ TLOJ;
                                      LFLANENT.NUMLANC  EQ TLANC;
                                      LFLANENT.ALIQUOTA EQ TALQ1
   [~FOUND] BEGIN
     SHOW (MENSAGEM("Lancamento nao Esta Cadastrado, tecle algo"))
     GOTO ALIQUOTA
   END

   FINDKEY EQ CPCADFOR BY INDEX.1 FOR CPCADFOR.COD_FOR EQ LFLANENT.CFORN
   ENTDISPLAY CPCADFOR
   ENTDISPLAY LFLANENT

   ENTRY LFLANENT.DOCTO

   MOVE TLOJ TO VLOJA

  FORNEC:
   INDICATE ENT$QUERY FALSE
   ENTRY CPCADFOR.COD_FOR   {AUTOFIND}
   INDICATE CERTO AS [FOUND]
   [~CERTO] ENTRY CPCADFOR.DESCR           {AUTOFIND,CAPSLOCK}
   [~FOUND] GOTO FORNEC
  CFO:
   INDICATE ENT$QUERY FALSE
   ENTRY LFCADCFO.CODIGO               {AUTOFIND}
   INDICATE CERTO AS [ FOUND]
   [~CERTO] ENTRY LFCADCFO.DESCR       {CAPSLOCK,AUTOFIND}
   [~FOUND] GOTO CFO

   ENTRY LFLANENT.ESPECIE  {CAPSLOCK}
   ENTRY LFLANENT.SERIE    {CAPSLOCK}
   MODELO:
   ENTRY LFLANENT.MODELO    {REQUIRED}
   IF (TMODELO NE 24 AND TMODELO NE 14 AND TMODELO NE 15 AND TMODELO NE 16) BEGIN
      IF (TMODELO NE 13 AND TMODELO NE 10 AND TMODELO NE 11 AND TMODELO NE 09) BEGIN
         IF (TMODELO NE 08 AND TMODELO NE 17 AND TMODELO NE 25 AND TMODELO NE 01) BEGIN
            IF (TMODELO NE 06 AND TMODELO NE 03 AND TMODELO NE 21 AND TMODELO NE 04) BEGIN
               IF (TMODELO NE 22 AND TMODELO NE 07 AND TMODELO NE 02 AND TMODELO NE 20 AND TMODELO NE 18) BEGIN
                  IF (TMODELO NE 55 AND TMODELO NE 57) BEGIN
                     CLEARXY SCREENEND 00
                     PAUSE "Cขdigo invlido, tecle <F1> para ver os cขdigos vlidos!"
                     CLEARXY SCREENEND 00
                     GOTO MODELO
                  END
               END
            END
         END
      END
   END

   ENTRY LFLANENT.DENTRADA {REQUIRED} 
   ENTRY LFLANENT.DEMISSAO TDT_EMIS {REQUIRED}
   
  UF:
   INDICATE ENT$QUERY FALSE  
   ENTRY AVCADEST.COD_EST TEST       {AUTOFIND}
   INDICATE CERTO AS [ FOUND]
   [~CERTO] ENTRY AVCADEST.DESCR TDEST {CAPSLOCK,AUTOFIND}
   [~FOUND] GOTO UF

  CTB:
   //ENTRY LFCADCTB.CODIGO TTIPCTB     {AUTOFIND}
   //INDICATE CERTO AS [ FOUND]
   //[~CERTO] ENTRY LFCADCTB.DESCR TDTIPCTB {CAPSLOCK,AUTOFIND}
   //[~FOUND] GOTO CTB
   ENTRY LFTIPCTB.COD_TIPCTB TTIPCTB {AUTOFIND}
   INDICATE EXISTE AS [FOUND]
   [NOT EXISTE] CLEARFORM TDTIPCTB
   [NOT EXISTE] ENTRY LFTIPCTB.DESCR TDTIPCTB {CAPSLOCK,AUTOFIND}
   [NOT FOUND ] GOTO CTB

   MOVE 0 TO VTBCICMS
   MOVE 0 TO VTISENTAS
   MOVE 0 TO VTOUTRAS
   MOVE 0 TO VTIMPOSTO
   MOVE 0 TO VTCONTABI
   MOVE 0 TO VTCALIPI
   MOVE 0 TO VTISENTA
   MOVE 0 TO VTOUTRA
   MOVE 0 TO VTIMP
 
   //===============  LANCAMENTO DO ICMS  =======================

   MOVE 0 TO VNUMLANC
   MOVE 0 TO VLANC1

   ENTRY LFLANENT.VL_BCICMS  TBASE_ICM
   IF TALQ3 NE 0 BEGIN
      MOVE TBASE_ICM TO VBASE
      CALC (TBASE_ICM * TALQ3 / 100) TO TBASE_ICM
      CALC (VBASE - TBASE_ICM) TO TISEN_ICM
   END
   ENTRY LFLANENT.VL_ISENTAS TISEN_ICM
   ENTRY LFLANENT.VL_OUTRAS  TOUT_ICM

   CALC ((TBASE_ICM * TALQ1) / 100) TO TIMP_ICM
   ENTRY LFLANENT.VL_IMPOSTO TIMP_ICM
   CALC (TBASE_ICM + TISEN_ICM + TOUT_ICM) TO TVLR_CONT
   ENTRY LFLANENT.VL_CONTABI TVLR_CONT

   ENTRY LFLANENT.TIPO TTIPO    {CHECK="123"}

  OBS:
   ENTRY LFOBSLAN.CODIGO TOBS {AUTOFIND}
   INDICATE CERTO AS [ FOUND]
   [~CERTO] ENTRY LFOBSLAN.DESCR TDOBS {AUTOFIND}
   [~FOUND] GOTO OBS
   
   MOVE "S" TO TCONF
   ACCEPT TCONF {CAPSLOCK}
   IF TCONF NE "S" GOTO ALIQUOTA

   //==========================  LANCAMENTO DO IPI  =======================

    IF LFPARAM.IPIENTRA EQ "N" GOTO TOT
   
    INDICATE FICA TRUE

    SEARCHKEY GE LFLANENI BY INDEX.1 COMPARING LFLANENI.LOJA     EQ TLOJ;
                                               LFLANENI.NUMLANCI EQ TLANC
                                               

    WHILE [ FICA]
      INDICATE PASSOU FALSE
      INDICATE ENTRA AS [ FOUND] 
     CLASS:
      CLEARFORM TCLASS
      CLEARFORM TUN
      CLEARFORM TQTD
      CLEARFORM TBASE_IPI
      CLEARFORM TISEN_IPI
      CLEARFORM TOUT_IPI
      CLEARFORM TALIQ_IPI
      CLEARFORM TIMP_IPI
      CLEARFORM TVLR_ART
   
      [ ENTRA] BEGIN
        IF LFLANENI.ALIQ_ICM NE LFLANENT.ALIQUOTA GOTO PROXIMO
      
        ENTDISPLAY LFLANENI
        ENTRY LFLANENI.CLASS TCLASS {AUTOFIND,DISPLAYONLY}
      END
      [~ENTRA] BEGIN
        ENTRY LFLANENI.CLASS TCLASS {REQUIRED}
        IF TCLASS EQ 0 GOTO TOT
      
        FINDKEY EQ LFLANENI BY INDEX.1 FOR LFLANENI.LOJA     EQ TLOJ;
                                           LFLANENI.NUMLANCI EQ TLANC;
                                           LFLANENI.ALIQ_ICM EQ LFLANENT.ALIQUOTA;
                                           LFLANENI.CLASS    EQ TCLASS

        INDICATE ENTRA AS [ FOUND]
        INDICATE PASSOU TRUE
        [ ENTRA] ENTDISPLAY LFLANENI
      END
 
      ENTRY LFLANENI.UP         TUN  {CAPSLOCK}
      ENTRY LFLANENI.QTDE       TQTD 
      ENTRY LFLANENI.B_CAL_IPI  TBASE_IPI
      ENTRY LFLANENI.VL_ISENTAS TISEN_IPI
      ENTRY LFLANENI.VL_OUTRAS  TOUT_IPI
      ENTRY LFLANENI.ALIQUOTA   TALIQ_IPI
      ENTRY LFLANENI.IMPOSTO    TIMP_IPI
      ENTRY LFLANENI.VL_ART82   TVLR_ART

      DIEF:
      INDICATE ENT$QUERY FALSE
      ENTRY LFCADDIE.CODIGO           TCODDIE { AUTOFIND, AUTOCLEAR }
      INDICATE EXISTE AS [ FOUND]
      [~EXISTE] CLEARFORM TDDIE
      [~EXISTE] ENTRY LFCADDIE.DESCR1 TDDIE   { CAPSLOCK, AUTOFIND }
      [~ FOUND] GOTO DIEF

      
      MOVE "S" TO TCONF
      ACCEPT TCONF {CAPSLOCK}
      IF TCONF NE "S" GOTO CLASS
      
      REREAD
      [~ENTRA] BEGIN
        CLEAR LFLANENI
        MOVE TLOJ   TO LFLANENI.LOJA
        MOVE TLANC  TO LFLANENI.NUMLANCI
        MOVE TCLASS TO LFLANENI.CLASS
        MOVE LFLANENT.ALIQUOTA TO LFLANENI.ALIQ_ICM
      END

      MOVE TUN       TO LFLANENI.UP
      MOVE TALIQ_IPI TO LFLANENI.ALIQUOTA
      MOVE TVLR_ART  TO LFLANENI.VL_ART82 
      MOVE TQTD      TO LFLANENI.QTDE
      MOVE TBASE_IPI TO LFLANENI.B_CAL_IPI
      MOVE TISEN_IPI TO LFLANENI.VL_ISENTAS
      MOVE TOUT_IPI  TO LFLANENI.VL_OUTRAS
      MOVE TIMP_IPI  TO LFLANENI.IMPOSTO
      SAVERECORD LFLANENI
      UNLOCK
      
     PROXIMO:
      [ PASSOU] BEGIN
        INDICATE ENTRA FALSE
        GOTO CLASS
      END     
  
      FIND GT LFLANENI BY INDEX.1
      [ FOUND] INDICATE FOUND AS LFLANENI.LOJA     EQ TLOJ
      [ FOUND] INDICATE FOUND AS LFLANENI.NUMLANCI EQ TLANC
    END

    // ======================  MOSTRA O TOTAL  ========================= 
   TOT:
    CLEAR LFLANENI 
    RETURN
    
    ENTER.SAVE:
      MOVE TALQ1 TO LFLANENT.ALIQUOTA
      LEFT TDT_ENT TO VDIA 2
      IF VDIA GT 15 MOVE 1 TO LFLANENT.DIA

      //joga os dados da janela de substituicao tributaria para o arquivo
      IF ((TCFO NE 1403) AND (TCFO NE 2403)) BEGIN
         CLEARFORM IMG_DADOS_SUBST_TRIB
      END
      MOVE TVLRIPI       TO LFLANENT.VLR_IPI
      MOVE TDESPACESS    TO LFLANENT.DESP_ACESSORIAS
      MOVE TBCICMSPROP   TO LFLANENT.BC_ICMS_PROP
      MOVE TICMSPROP     TO LFLANENT.ICMS_PROP
      MOVE TBCSUBSTRIB   TO LFLANENT.BC_SUBST_TRIB

      RETURN
ENTEREND
ABORT

KEYPROC KEY.ESCAPE
  IF CURRENT_WINDOW GE 19 BEGIN
     ENTAGAIN
     RETURN
  END
  CLEARSCREEN
  ABORT

KEYPROC KEY.CLEAR
INDICATE CERTO AS CURRENT_WINDOW EQ 42
[ CERTO] BEGIN
  ENTAGAIN
  RETURN
END  
[~CERTO] INDICATE CERTO AS CURRENT_WINDOW EQ 22
[~CERTO] INDICATE CERTO AS CURRENT_WINDOW EQ 25
[~CERTO] BEGIN
  FOR X FROM 27 TO 37 STEP 2
      [~CERTO] INDICATE CERTO AS CURRENT_WINDOW EQ X
  LOOP
END
[ CERTO] RETURN CLASS
RETURN INICIO

KEYPROC KEY.DELETE
INDICATE CERTO AS CURRENT_WINDOW EQ 22 
[~CERTO] INDICATE CERTO AS CURRENT_WINDOW EQ 25
[~CERTO] BEGIN
  FOR X FROM 27 TO 37 STEP 2
      [~CERTO] INDICATE CERTO AS CURRENT_WINDOW EQ X
  LOOP
END
[ CERTO] BEGIN
  FINDKEY EQ LFLANENI BY INDEX.1 FOR LFLANENI.LOJA     EQ TLOJ;
                                     LFLANENI.NUMLANCI EQ TLANC;
                                     LFLANENI.ALIQ_ICM EQ LFLANENT.ALIQUOTA;
                                     LFLANENI.CLASS    EQ TCLASS
  [ FOUND] BEGIN
    LOCK 
    DELETE LFLANENI
    UNLOCK
    RETURN PROXIMO
  END
END                                         
ENTAGAIN
RETURN

KEYPROC KEY.UP
  INDICATE CERTO AS CURRENT_WINDOW EQ 22
  [ CERTO] ENTAGAIN
  [~CERTO] BACKFIELD
RETURN

KEYPROC KEY.HELP
  IF CURRENT_WINDOW EQ 17 BEGIN
     PAGE HELP1
     PAUSE ""
     PAGE HELP2
     PAUSE ""
     ENTAGAIN
     RETURN
  END
  PAGE HELP
  PAUSE ""
  ENTAGAIN
RETURN

KEYPROC KEY.SAVE
KEYPROC KEY.USER
KEYPROC KEY.USER2
KEYPROC KEY.CALCULATE
KEYPROC KEY.FIELD
  ENTAGAIN
RETURN

KEYPROC KEY.PRINT
   PAGE TELA_CAD_AUX
   LTELAAUX:
   CLEARFORM TELA_CAD_AUX
   MOVE 1 TO TELA_CAD_AUX.1
   ACCEPT TELA_CAD_AUX.1
   [KEY.ESCAPE] BEGIN
      ENTAGAIN
      RETURN
   END
   IF TELA_CAD_AUX.1 LE 0 GOTO LTELAAUX
   IF TELA_CAD_AUX.1 EQ 1 BEGIN
      IF ((TCFO NE 1403) AND (TCFO NE 2403)) BEGIN
         CLEARXY SCREENEND 00
         PAUSE "Somente pode-se informar dados da substit. tributaria para os CFOs 1403 ou 2403."
         CLEARXY SCREENEND 00
         GOTO LTELAAUX
      END 
      PAGE IMG_DADOS_SUBST_TRIB
      MOVE LFLANENT.VLR_IPI         TO TVLRIPI
      MOVE LFLANENT.DESP_ACESSORIAS TO TDESPACESS
      MOVE LFLANENT.BC_ICMS_PROP    TO TBCICMSPROP
      MOVE LFLANENT.ICMS_PROP       TO TICMSPROP      
      MOVE LFLANENT.BC_SUBST_TRIB   TO TBCSUBSTRIB    
   
      INF_SUBST_TRIB:
      ACCEPT TVLRIPI          {AUTOCLEAR}
      [KEY.ESCAPE] GOTO VAI_VOLTAR
      [KEY.UP] GOTO INF_SUBST_TRIB
      [KEY.DOWN] GOTO INF_SUBST_TRIB

      DESPESAS_ACESSORIAS:
      ACCEPT TDESPACESS       {AUTOCLEAR}
      [KEY.ESCAPE] GOTO VAI_VOLTAR
      [KEY.UP] GOTO INF_SUBST_TRIB
      [KEY.DOWN] GOTO DESPESAS_ACESSORIAS

      BASE_CALCULO_ICMS_PROPRIO:
      ACCEPT TBCICMSPROP      {AUTOCLEAR}
      [KEY.ESCAPE] GOTO VAI_VOLTAR
      [KEY.UP] GOTO DESPESAS_ACESSORIAS
      [KEY.DOWN] GOTO BASE_CALCULO_ICMS_PROPRIO

      ICMS_PROPRIO:
      ACCEPT TICMSPROP        {AUTOCLEAR}
      [KEY.ESCAPE] GOTO VAI_VOLTAR
      [KEY.UP] GOTO BASE_CALCULO_ICMS_PROPRIO
      [KEY.DOWN] GOTO ICMS_PROPRIO

      BASE_CALCULO_SUBSTITUICAO_TRIBUTARIA:
      ACCEPT TBCSUBSTRIB      {AUTOCLEAR}
      [KEY.ESCAPE] GOTO VAI_VOLTAR
      [KEY.UP] GOTO ICMS_PROPRIO
      [KEY.DOWN] GOTO BASE_CALCULO_SUBSTITUICAO_TRIBUTARIA

      MOVE "S" TO TCONF_SUBST_TRIB
      VOLTA_CONFIRMA:
      ACCEPT TCONF_SUBST_TRIB {CAPSLOCK}
      [KEY.ESCAPE] GOTO VAI_VOLTAR
      [KEY.UP] GOTO BASE_CALCULO_SUBSTITUICAO_TRIBUTARIA
      [KEY.DOWN] GOTO VOLTA_CONFIRMA
      IF ((TCONF_SUBST_TRIB NE "S") AND (TCONF_SUBST_TRIB NE "N")) GOTO VOLTA_CONFIRMA
      IF TCONF_SUBST_TRIB NE "S" GOTO INF_SUBST_TRIB
   
      MOVE TVLRIPI       TO LFLANENT.VLR_IPI
      MOVE TDESPACESS    TO LFLANENT.DESP_ACESSORIAS
      MOVE TBCICMSPROP   TO LFLANENT.BC_ICMS_PROP
      MOVE TICMSPROP     TO LFLANENT.ICMS_PROP
      MOVE TBCSUBSTRIB   TO LFLANENT.BC_SUBST_TRIB
   END
   VAI_VOLTAR:
   ENTAGAIN
RETURN
